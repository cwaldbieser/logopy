
to abs.value :x
op ifelse [ :x < 0 ] [ -1 * :x ] [ :x ]
end

to max :a :b
ifelse [ :a >= :b ] [ op :a ] [ op :b ]
end

to min :a :b
ifelse [ :a <= :b ] [ op :a ] [ op :b ]
end

to logo2cartesian.angle :theta
localmake "alpha (sum (product :theta -1) 90)
if [ :alpha < 0.0 ] [ localmake "alpha sum 360 :alpha ]
op :alpha
end

to arc.from :r [:angle 360] [:clockwise "true] [:step 15] [:squash.x 1] [:squash.y 1]
localmake "step abs.value :step
localmake "start.pos pos
localmake "h first :start.pos
localmake "k last :start.pos
; (print [Turtle start position:] :h :k)
localmake "theta logo2cartesian.angle heading
; (print [Turtle start heading:] :theta)

; Center should be either one radius to the left or right, depending on the direction of the angle sweep.
ifelse [ :clockwise ] [ localmake "alpha :theta + 90 ] [ localmake "alpha :theta - 90 ]
localmake "i :r * (cos :alpha) + :h
localmake "j :r * (sin :alpha) + :k
; (print [Center of the circle is at:] :i :j)

; Get into position.
pu
setpos (list :i :j)
localmake "theta logo2cartesian.angle towards :start.pos
localmake "theta.start :theta
; (print [Will start sweeping out angle at heading:] :theta)
make "x :i + :squash.x * :r * (cos :theta)
make "y :j + :squash.y * :r * (sin :theta)
; (print [Initial X,Y] :x :y)
setpos (list :x :y)

; Start drawing.
pd
do.until [
    ifelse [ :clockwise ] [
        make "theta :theta + :step
    ] [
        make "theta :theta - :step
    ]
    make "x :i + :squash.x * :r * cos :theta
    make "y :j + :squash.y * :r * sin :theta  
    setpos (list :x :y)
] [((max :theta :theta.start) - (min :theta :theta.start)) >= :angle]
; (print "final.theta :theta "theta.start :theta.start)
; (print (abs.value (:theta - :theta.start)) "vs :angle)
end

to snake

pu lt 90 fd 50 pd fd 150
pu back 40 rt 90 fd 60 
pd arc 360 20
pu
back 60 lt 90 fd 40 pd
rt 90 pd fd 75
(arc.from 150 180 "false 15 1 0.25)
rt 90
rt 90
fd 160
(arc.from 100 90 "false)
rt 90
fd 200
(arc.from 100 90)
lt 90
fd 75
rt 90
fd 50
(arc.from 150 180 "false 15 0.25 1)
rt 180
fd 300
lt 90
fd 60

end

setspeed 5
setpensize 5
cs
setpencolor "blue
pu fd 200 rt 90 fd 80 lt 90 pd
filled "blue [snake]
pu home
pu rt 180 fd 200 rt 90 fd 80 lt 90 pd
setpencolor "yellow
filled "yellow [snake]
pu home setpencolor "green pd 
